# Package `nog-error`

The package `nog-error` helps handling errors.

Meteor provides a mechanism to prevent server-side errors from being sent to the
client: It sends `Meteor.Error` as is.  Errors of a different type, however, are
only reported to the server log.  Either a generic error 500 is sent to the
client instead or the content of the field `sanitizedError` if it contains
a `Meteor.Error`.

`nog-error` provides functions to maintain four information streams: `{full,
sanitized} x {user, developer}`.  `reason` contains information that can be
displayed to users as is.  `details` contains information for developers.  The
error type `NogError.Error` is used on the server.  The `sanitizedError` is
reported to the client.

Errors are stored upon creation to the Mongo collection `nogerror.errorLog` with
a TTL index (see <http://docs.mongodb.org/manual/core/index-ttl/>) that removes
error documents after some time (default: 5 days).  Only server-side errors are
currently stored.  Errors have a short `token`, which can be useful to retrieve
them from the log:

```{.javascript}
db.nogerror.errorLog.findOne({token: 'ma4ddq'})
```

Errors are created from a spec.  It is an open question whether we should use
many detailed specs or use fewer general purpose specs and report details in the
reason.  See `nog-error-specs.coffee` for a list of error specs.

`nog-error` also provides a default error handler and a template for displaying
client-side errors.

## `NogError.createError(spec, context)` (anywhere)

`createError(spec, context)` creates an error object.  It uses the general
structure from `spec` and the situation-specific information from `context`.  If
`context` has a field `cause`, it will be used to derive an error history, which
simply is an array of the previous causes.  The error object is of type
`NogError.Error` with a `sanitizedError` of type `Meteor.Error` that would be
reported to the client.

`spec` is usually one of the specs that are defined in `nog-error-specs.coffee`.
The full developer documentation contains a list in a separate section below.
The format of `spec`, which is needed to define a new error, is described below.

`context` is an object that may be used to provide information about the context
in which the error occurred.  The context information will be used when creating
the error object and also be stored in the error log:

 - `reason` (String, optional): A message for the user that overrides the
   default message from the specs.
 - `details` (String, optional): A detailed message to developers that
   overrides the default message from the specs.
 - `cause` (Error, optional): Will be used to derive an error history.
 - Some specs require further fields that are used to construct the messages.

Usage examples:

```{.coffee}
{
  ERR_BLOB_UPLOAD
  ERR_LOGIC
  ERR_UNKNOWN_USERID
  createError
  nogthrow
} = NogError


fn = ->
  ...
  if not found
    nogthrow ERR_UNKNOWN_USERID, {uid}
  ...
  nogthrow ERR_LOGIC, {reason: 'Unknown entry type.'}


someAction (err, res) ->
  if err
    @onerror createError ERR_BLOB_UPLOAD,
      cause: err
      reason: "
          Failed to get upload URL for part #{part.partNumber}; aborting
          after #{@nTries} tries.
        "
```

New error codes are defined (by convention in `nog-error-specs.coffee`) with
a `spec` object with the following fields:

 - `errorCode` (`String`, by convention all uppercase `ERR_<topic>_<details>`)
   identifies the type of error.
 - `statusCode` (`Number`) is a HTTP status code that is used if the error is
   reported via HTTP (from the REST API).
 - `reason` (`String | (context) -> String`): Text that explains the reasons
   (for a user); either static or generated by a function.
 - `details` (`String | (context) -> String`): Text that explains the details
   (for a developer); either static or generated by a function.
 - `contextPattern` (`Match Pattern`): A Meteor match pattern that is used to
   validate the `context` object.  A warning is reported to the console, if the
   pattern does not match.
 - `sanitized` (`null | 'full' | Object`) controls how the sanitized error for
   the client is constructed.  For `null`, an unspecified error with code
   `NOGERR` will be used.  For `full`, the complete information that is
   available on the server will be used.  `sanitized` can also be an Object with
   the following optional fields `{errorCode: String, reason: String | (context)
   -> String, details: String | (context) -> String}`) to control the details.

Example specs:

```{.coffee}
  {
    errorCode: 'ERR_APIKEY_DELETE'
    statusCode: 403
    sanitized: 'full'
    reason: 'Failed to create API key.'
  }
  {
    errorCode: 'ERR_S3_CREATE_MULTIPART'
    statusCode: 502
    sanitized: null
    reason: 'Failed to create S3 multipart upload.'
    details: (ctx) -> "
        Failed to call createMultipartUpload with S3 bucket `#{ctx.s3Bucket}`,
        and object key `#{ctx.s3ObjectKey}`.
      "
    contextPattern:
      s3Bucket: String
      s3ObjectKey: String
  }
```

## `NogError.nogthrow(spec, context)` (anywhere)

`nogthrow()` calls `createError()` and throws the error.

Usage example:

```{.coffee}
{
  nogthrow
  ERR_BLOB_ABORT_PENDING
} = NogError

someFunction = (opts) ->
  try
    doSomething()
  catch cause
    nogthrow ERR_BLOB_ABORT_PENDING, {sha1: opts.sha1, cause}
```

## `NogError.defaultErrorHandler(err)` (anywhere)

An error handler that stores the error for `{{> errorDisplay}}`.

Example usage with method call on client:

```{.coffee}
aFunction = ->
  ...
  Meteor.call 'something', (err, res) ->
    if err?
      return NogError.defaultErrorHandler(err)
    ...
```

Packages typically call the handler indirectly via a configurable hook:

```{.coffee}
NogBlob =
  onerror: NogError.defaultErrorHandler
  ...

aFunction = ->
  ...
  Meteor.call 'something', (err, res) ->
    if err?
      NogBlob.onerror(err)
```

## `{{> errorDisplay}}` (client)

A Meteor template that displays the errors that have been reported to the error
handler.

## Error specs

See `nog-error-specs.coffee` for a list of error specs.
